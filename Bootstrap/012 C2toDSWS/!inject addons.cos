*NOTE: THIS FILE ONLY RUNS ONCE, WHEN CREATING THE WORLD!



inst

new: simp 3 14 50203 "blnk" 0 0 0

mesg writ targ 1000






scrp 3 14 50203 1000

	lock




******* ALL ADDON INSTALLER RELATED STRINGS *******

	inst

*catalogue tag to read strings from
	sets va20 "WorldSwitcher_AddonInstaller_Text"

*might as well reload catalogue files
	reaf

*read in strings from specified catalogue file
	doif reaq va20 = 1

		sets mame "UI_FAILEDTOINJECT_OPEN" read va20 0
		sets mame "UI_FAILEDTOINJECT_CLOSE" read va20 1
		sets mame "UI_SCRIPTNOTFOUND" read va20 2
		sets mame "UI_SCRIPTERROR" read va20 3
		sets mame "UI_DEPENDENCYERROR" read va20 4
		sets mame "UI_CHUNKNOTFOUND" read va20 5

*plain english fallback in case the catalogue file is missing
	else

		sets mame "UI_FAILEDTOINJECT_OPEN" "Failed to inject '"
		sets mame "UI_FAILEDTOINJECT_CLOSE" "'."
		sets mame "UI_SCRIPTNOTFOUND" "Script not found: Type 'dbg: poll' into CAOS Command Line for details."
		sets mame "UI_SCRIPTERROR" "Script error: Type 'dbg: poll' into CAOS Command Line for details."
		sets mame "UI_DEPENDENCYERROR" "'Error: Dependency evaluation failed."
		sets mame "UI_CHUNKNOTFOUND" "Error: Chunk not found. Make sure the .agent file is in 'My Agents'."

	endi



	file iope 0 "addons"

	doif inok = 1

		loop

			sets va99 innl

*wait 1 before every inject attempt. we don't want to be creating multiple maps at the same time.
			wait 1

			gsub inject_it


		untl inok = 0

	endi

*dbg: outs "done!! :)"

	kill ownr





*INJECTION SUBROUTINE

	subr inject_it

		inst

		doif va99 = ""
			stop
		endi

*test the chunk exists first
		setv va00 pray test va99


*	Inject the chunk
		doif va00 <> 0
			setv va00 pray injt va99 1 va01
			doif va00 <> 0
				gsub inject_failed
			else
				dbg: outs "Worldswitcher: Agent '"
				dbg: outs va99
				dbg: outs "' injected successfully."
			endi
		else
			setv va00 -4
			gsub inject_failed
		endi
	retn





*INJECTION FAILED SUBROUTINE

	subr inject_failed

*create Injection Failed dialog even if we already have one (they can stack)

*plane is decided by adding 1 to initial PLNE value for each existing error message (stops intersections)
		setv va20 9076
		addv va20 totl 3 14 50203

		new: comp 3 14 50203 "small_useful_screen_1button" 1 0 va20

*Invisible, Floatable, Camera Shy
		attr 304

*Looking for quit dialog, we delete ours when that appears
		tick 1

*the OK button
		pat: butt 2 "small_useful_screen_1button" 1 2 126 107 2 [0 1 255] 1001 0
		part 2
		anim [0]

*the text box
		pat: fixd 3 "small_useful_screen_1button" 3 25 38 1 "WhiteOnTransparentChars"

*lower text box
		pat: fixd 4 "small_useful_screen_1button" 3 25 74 1 "WhiteOnTransparentChars"

		part 3
		frmt 5 0 5 0 8 0 2




****TEXT****

*"Failed to inject '"
		sets va15 mame "UI_FAILEDTOINJECT_OPEN"
*<addon name>
		adds va15 va99
*"'."
		adds va15 mame "UI_FAILEDTOINJECT_CLOSE"

		ptxt va15

*"Script not found: Type 'dbg: poll' into CAOS Command Line for details."
		doif va00 = -1
			sets va16 mame "UI_SCRIPTNOTFOUND"
*"Script error: Type 'dbg: poll' into CAOS Command Line for details."
		elif va00 = -2
			sets va16 mame "UI_SCRIPTERROR"
*"'Error: Dependency evaluation failed."
		elif va00 = -3
			sets va16 mame "UI_DEPENDENCYERROR"
*"Error: Chunk not found. Make sure the .agent file is in 'My Agents'."
		else
			sets va16 mame "UI_CHUNKNOTFOUND"
		endi

		part 4
		frmt 5 0 5 0 8 0 2

		ptxt va16


*DEBUG TEXT (verbose error)
		sets va17 "\n"
		adds va17 "Worldswitcher Addon Injector Error ("
		adds va17 va99
		adds va17 "):\n\n"

		doif va00 = -4
			adds va17 "Chunk not found."
		else
			adds va17 va01
		endi

		adds va17 "\n"

		dbg: outs va17



************


*center it

		setv va90 wndw
		divv va90 2
		setv va91 wndh
		divv va91 2
		setv va92 wdth
		divv va92 2
		setv va93 hght
		divv va93 2

		subv va90 va92
		subv va91 va93

		flto va90 va91


	retn

endm



******************************************************************************************
****************************** ERROR MESSAGE BOX SCRIPTS! ********************************
******************************************************************************************

*clicked ok
scrp 3 14 50203 1001
	sndc "lg_1"
	kill ownr
endm

*re-center when window resized
scrp 3 14 50203 123

	setv va90 wndw
	divv va90 2
	setv va91 wndh
	divv va91 2
	setv va92 wdth
	divv va92 2
	setv va93 hght
	divv va93 2

	subv va90 va92
	subv va91 va93

	flto va90 va91

endm


*constantly check for quit dialog, and delete our dialog if quit dialog exists
scrp 3 14 50203 9
	doif totl 1 2 8 > 0
		kill ownr
	endi
endm



*see ya!

rscr

enum 3 14 50203
	kill targ
next

scrx 3 14 50203 1001
scrx 3 14 50203 123
scrx 3 14 50203 9